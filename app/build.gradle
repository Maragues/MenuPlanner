buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}


Properties firebaseProperties = new Properties()
firebaseProperties.load(project.rootProject.file('firebase.properties').newDataInputStream())
def firebaseServerId = firebaseProperties.getProperty('firebase.serverId')

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.maragues.menu_planner"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SERVER_CIENT_ID", '"'+firebaseServerId+'"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v4:' + rootProject.ext.supportLibVersion
    compile 'com.android.support:appcompat-v7:' + rootProject.ext.supportLibVersion
    compile 'com.android.support:design:' + rootProject.ext.supportLibVersion

    compile 'com.google.firebase:firebase-database:' + rootProject.ext.playServicesVersion
    compile 'com.google.firebase:firebase-auth:' + rootProject.ext.playServicesVersion
    compile 'com.google.android.gms:play-services-auth:' + rootProject.ext.playServicesVersion

    compile 'com.github.quivr:android-week-view:2.0.2'

    compile "com.jakewharton.threetenabp:threetenabp:1.0.4"
    provided "com.jakewharton.threetenabp:threetenabp:1.0.4"

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.google.dagger:dagger:HEAD-SNAPSHOT'
    annotationProcessor 'com.google.dagger:dagger-compiler:HEAD-SNAPSHOT'

    compile 'io.reactivex.rxjava2:rxjava:2.0.3'

    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    provided 'com.google.auto.value:auto-value:1.3'
    annotationProcessor 'com.google.auto.value:auto-value:1.3'
    annotationProcessor 'com.gabrielittner.auto.value:auto-value-with:1.0.0'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
//    compile 'com.ryanharter.auto.value:auto-value-parcel-adapter:0.2.5'
    annotationProcessor 'me.mattlogan.auto.value:auto-value-firebase:1.0.1'
    provided 'me.mattlogan.auto.value:auto-value-firebase-annotation:1.0.1'
    provided 'javax.annotation:jsr250-api:1.0'

    def thirtyinchVersion = '0.8.0-rc2'
    // MVP for activity and fragment
    compile "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion"

    // test extension
    testCompile "net.grandcentrix.thirtyinch:thirtyinch-test:$thirtyinchVersion"

    debugCompile 'com.amitshekhar.android:debug-db:0.4.0'

    debugCompile('com.squareup.leakcanary:leakcanary-android:' + rootProject.ext.leakCanaryVersion)
    releaseCompile('com.squareup.leakcanary:leakcanary-android-no-op:' + rootProject.ext.leakCanaryVersion)
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:' + rootProject.ext.leakCanaryVersion
    androidTestCompile 'com.squareup.leakcanary:leakcanary-android-no-op:' + rootProject.ext.leakCanaryVersion

    testCompile 'junit:junit:' + rootProject.ext.junitVersion

    testCompile "org.mockito:mockito-core:2.5.3"

    androidTestCompile 'com.android.support:support-annotations:' + rootProject.ext.supportLibVersion
    androidTestCompile 'com.android.support.test:runner:' + rootProject.ext.runnerVersion
    androidTestCompile 'com.android.support.test:rules:' + rootProject.ext.rulesVersion
    androidTestCompile('com.android.support.test.espresso:espresso-core:' + rootProject.ext.espressoVersion) {
        exclude group: 'com.android.support'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-intents:' + rootProject.ext.espressoVersion) {
        exclude group: 'com.android.support'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-contrib:' + rootProject.ext.espressoVersion) {
        exclude group: 'com.android.support'
    }
}

apply plugin: 'com.google.gms.google-services'